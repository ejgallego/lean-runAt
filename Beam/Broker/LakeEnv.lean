/-
Copyright (c) 2026 Lean FRO LLC. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Emilio J. Gallego Arias
-/

import Lake.Config.Env
import Lake.Config.InstallPath
import Lake.CLI.Serve
import Lake.Load.Workspace
import Beam.Broker.LakeHelper

open System
open Std
open Lean

namespace Beam.Broker

open Lake

private def computeLakeEnv (leanCmd? : Option String) : IO Lake.Env := do
  let elan? ← Lake.findElanInstall?
  let lean? ←
    match leanCmd? with
    | some leanCmd =>
        if leanCmd.trimAscii.isEmpty then
          pure none
        else
          Lake.findLeanCmdInstall? leanCmd
    | none =>
        pure none
  let (lean?, lake?) ←
    match lean? with
    | some lean => pure (some lean, some (Lake.LakeInstall.ofLean lean))
    | none =>
        let (_, lean?, lake?) ← Lake.findInstall?
        pure (lean?, lake?)
  let some lean := lean?
    | throw <| IO.userError "could not locate Lean installation for Lake workspace loading"
  let some lake := lake?
    | throw <| IO.userError "could not locate Lake installation for workspace loading"
  match ← (Lake.Env.compute lake lean elan?).toBaseIO with
  | .ok env => pure env
  | .error err => throw <| IO.userError s!"failed to compute Lake environment: {err}"

/-- A `Workspace` contains live Lean values, so only load it across an exact Lean ABI match. -/
private def canLoadWorkspaceInProcess (lakeEnv : Lake.Env) : Bool :=
  !lakeEnv.lean.githash.isEmpty && lakeEnv.lean.githash == Lean.githash

private def detectConfigFile? (root : FilePath) : IO (Option (FilePath × FilePath)) := do
  let leanConfig := root / "lakefile.lean"
  if ← leanConfig.pathExists then
    pure <| some (System.FilePath.mk "lakefile.lean", leanConfig)
  else
    let tomlConfig := root / "lakefile.toml"
    if ← tomlConfig.pathExists then
      pure <| some (System.FilePath.mk "lakefile.toml", tomlConfig)
    else
      pure none

private def detectConfigFile (root : FilePath) : IO (FilePath × FilePath) := do
  match ← detectConfigFile? root with
  | some config => pure config
  | none => throw <| IO.userError s!"could not find lakefile.lean or lakefile.toml under {root}"

private def loadWorkspaceWithConfig (root : FilePath) (lakeEnv : Lake.Env)
    (relConfigFile configFile : FilePath) : IO (Option Workspace × Array String) := do
  let loadConfig : LoadConfig := {
    lakeEnv := lakeEnv
    wsDir := root
    relPkgDir := System.FilePath.mk "."
    pkgDir := root
    relConfigFile := relConfigFile
    configFile := configFile
    updateToolchain := false
  }
  let (ws?, log) ← LoggerIO.captureLog <| Lake.loadWorkspace loadConfig
  let messages := log.entries.map fun entry => entry.toString
  pure (ws?, messages)

private def loadWorkspaceFailureMessage
    (root : FilePath)
    (messages : Array String) : String :=
  let lines :=
    #[s!"failed to load Lake workspace at {root}"] ++
    (if messages.isEmpty then #[] else #["Lake log:"] ++ messages)
  String.intercalate "\n" lines.toList

inductive WorkspaceLoadResult where
  | loaded (workspace : Workspace)
  | leanBuildMismatch

def loadWorkspaceForRoot (root : FilePath) (leanCmd? : Option String) : IO WorkspaceLoadResult := do
  let (relConfigFile, configFile) ← detectConfigFile root
  let lakeEnv ← computeLakeEnv leanCmd?
  unless canLoadWorkspaceInProcess lakeEnv do
    return .leanBuildMismatch
  let (ws?, messages) ← loadWorkspaceWithConfig root lakeEnv relConfigFile configFile
  if let some ws := ws? then
    pure <| .loaded ws
  else
    throw <| IO.userError <| loadWorkspaceFailureMessage root messages

structure LeanServerLakeEnv where
  env : Array (String × Option String)
  moreServerArgs : Array String
  deriving FromJson, ToJson

private def leanServerLakeEnvInProcess
    (root : FilePath)
    (leanCmd? : Option String) : IO LeanServerLakeEnv := do
  let lakeEnv ← computeLakeEnv leanCmd?
  -- Always preserve the target runtime environment: the Beam plugin links against target Lean/Lake
  -- libraries even when there is no Lake configuration or this Lake version cannot load it.
  let fallback : LeanServerLakeEnv := {
    env := lakeEnv.vars
    moreServerArgs := #[]
  }
  if !canLoadWorkspaceInProcess lakeEnv then
    return fallback
  let some (relConfigFile, configFile) ← detectConfigFile? root
    | pure fallback
  let (ws?, messages) ← loadWorkspaceWithConfig root lakeEnv relConfigFile configFile
  if let some ws := ws? then
    pure {
      env := ws.augmentedEnvVars
      moreServerArgs := ws.root.moreGlobalServerArgs
    }
  else
    pure {
      env := lakeEnv.vars.push
        (Lake.invalidConfigEnvVar, some <| String.intercalate "\n" messages.toList)
      moreServerArgs := #[]
    }

def leanServerLakeEnv
    (root : FilePath)
    (leanCmd? : Option String)
    (lakeHelper? : Option FilePath := none) : IO LeanServerLakeEnv := do
  match lakeHelper?, leanCmd? with
  | some helper, some leanCmd =>
      match ← runLakeHelper helper "server-env" <| toJson ({
          root := root.toString
          leanCmd
        } : LakeHelperEnvRequest) with
      | .ok result =>
          match fromJson? result with
          | .ok serverEnv => pure serverEnv
          | .error err =>
              throw <| IO.userError s!"target Lake helper returned an invalid server environment: {err}"
      | .error failure => throw <| IO.userError failure.message
  | _, _ =>
      leanServerLakeEnvInProcess root leanCmd?

end Beam.Broker
